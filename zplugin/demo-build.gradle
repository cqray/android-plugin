buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.0.2"
        classpath 'com.jakewharton:butterknife-gradle-plugin:10.2.3'
        classpath "com.github.xiaojinzi123.Component:component-plugin:1.9.2.1-androidx-java8"
    }
}

// 引入版本管理插件
plugins { id "com.android.plugins" apply false }

allprojects {

    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects { project ->
    // 默认应用所有子项目中
    apply plugin: 'com.android.plugins'
    // 不同的模块应用不同的插件
    if (project.name == "app") {
        // 入口模块依赖application
        apply plugin: 'com.android.application'
        // 模块化插件
        apply plugin: 'com.xiaojinzi.component.plugin'
    } else {
        // 子模块依赖library和butterKnife
        apply plugin: 'com.android.library'
        apply plugin: 'com.jakewharton.butterknife'
    }

    // 自动处理框架
    processors {
        butterknife true
        component false
        lombok true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}